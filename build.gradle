// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"

        //SMSSDK
        classpath "com.mob.sdk:MobSDK:2018.0319.1724"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

String mavenUrl = "http://mvn.mob.com/android"
if (hasProperty("mavenUrl")) {
    mavenUrl = getProperty("mavenUrl")
}



allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url mavenUrl
        }
        maven {url 'https://dl.bintray.com/chaozhouzhang/maven' }
    }
    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}
ext {
    buildTool = findBuildTools()
}



task clean(type: Delete) {
    delete rootProject.buildDir
}

String findBuildTools() {
    def sdkDir = System.env.ANDROID_HOME;
    if(sdkDir == null) {
        def localProperties = new File(project.rootDir, "local.properties")
        if (localProperties.exists()) {
            Properties properties = new Properties()
            localProperties.withInputStream {
                instr->properties.load(instr)
            }
            sdkDir = properties.getProperty('sdk.dir')
        }
    }
    if (sdkDir != null) {
        def buildTools = new File(sdkDir, "build-tools")
        if (buildTools.exists()) {
            def tools = buildTools.list()
            if (tools != null) {
                Arrays.sort(tools)
                return tools[tools.length - 1]
            }
        }
    }
    return null
}
